///////////////////////////////////////////////////////////////////////////////
//  HUD Elements                                                             //
///////////////////////////////////////////////////////////////////////////////

hudbgcolor = 0x99000000

resethud = [
    if (! $mainmenu) [
        if $editing [ //if ingame and in edit mode
            showui "editstats"
            showui "varicons"
            hideui "hudfill"
            hideui "hud"
            hideui "gamebar"
            hideui "weaponbar"
            hideui "centerbar"
        ] [ //if ingame and not in edit mode
            showui "hudfill"
            showui "hud"
            showui "gamebar"
            showui "weaponbar"
            showui "centerbar"
            hideui "editstats"
            hideui "varicons"
        ]
    ] [ //if on the main menu hide everything
        hideui "hudfill"
        hideui "hud"
        hideui "gamebar"
        hideui "weaponbar"
        hideui "centerbar"
        hideui "editstats"
        hideui "varicons"
    ]
]

newui "hudfill" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace 0 0 [
            uivlist 0 [
                uihlist 0 [
                    uifill 1.475
                    uitriangle $hudbgcolor 0.105 0.0525 225
                    uicolor $hudbgcolor 0.35 0.075
                ]
                uihlist 0 [
                    uicolor $hudbgcolor 0.475 0.075
                    uitriangle $hudbgcolor 0.105 0.0525 135
                    uifill 0.8
                    uitriangle $hudbgcolor 0.105 0.0525 225
                    uicolor $hudbgcolor 0.475 0.075
                ]
            ]
        ]
    ]
]

newui "hud" [
    uiallowinput 0
    if $showhud [
        uialign -1 1
        uiabovehud
        uispace 0.02 0.017 [
            uivlist 0.01 [
                //uialign- -1 0
                //how this works: players have 10 hp so 10 individual uifills are created either
                //white (for hitpoints the player still has) or gray (for hitpoints lost)
                uihlist 0.005 [
                    uicolortext (format "%1" (max 0 (getclienthealth (getclientnum)))) 0xFFFFFF 0.7
                    uifill 0.005
                    loop i 10 [
                        if (> (getclienthealth (getclientnum)) $i) [
                            uicolor 0xFFFFFF 0.029 0.004 [
                                uioutline 0xFFFFFF ; uiclamp- 1 1 1 1
                            ]
                        ] [
                            uicolor 0xAA505050 0.029 0.004 [
                                uioutline 0xAA505050 ; uiclamp- 1 1 1 1
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

newui "gamebar" [
    uiallowinput 0
    if $showhud [
        uialign 0 -1
        uivlist 0 [
            uihlist 0 [
                uitriangle $hudbgcolor 0.069 0.0345 315
                uicolor $hudbgcolor 1 0 [
                    uispace $UIwindow_padding $UIwindow_padding [
                        uihlist 0 [
                            //difference between two teams score, normalized to the end of match points so it fits on a bar nicely
                            score = (divf (*f 0.5 (abs (format "%1" (- (getteamscore 2) (getteamscore 1))))) 500)
                            sign = (> (- (getteamscore 2) (getteamscore 1)) 0)
                            if (sign) [
                                uicolor 0x01000000 $score 0.03
                                uicolor 0x55FF0000 $score 0.03
                            ] [
                                uicolor 0x550000FF $score 0.03
                                uicolor 0x01000000 $score 0.03
                            ]
                        ]
                    ]
                ]
                uitriangle $hudbgcolor 0.069 0.0345 45
            ]
            uihlist -0.001 [
                uitriangle $hudbgcolor 0.07 0.035 315
                uicolor $hudbgcolor 0.1 0.0495 [
                    uitext (timeremaining 1) 0.8
                ]
                uitriangle $hudbgcolor 0.07 0.035 45
            ]
        ]
    ]
]

cooldownbar = [
    barcolor = 0x33FFFFFF
    if (<f (getweaponheat) 0.3) [
        barcolor = 0x01FFFFFF
    ] [
        if (<f (getweaponheat) 0.6) [
            barcolor = 0x11FFCCCC
        ] [
            if (<f (getweaponheat) 0.8) [
                barcolor = 0x22FF79999
            ] [
                if (<f (getweaponheat) 0.9) [
                    barcolor = 0x2CFF6666
                ] [
                    barcolor = 0x33FF0000
                ]
            ]
        ]
    ]
    uicircle $barcolor 0.06
]

newui "centerbar" [
    uiallowinput 0
    if $showhud [
        uialign 0 0
        uispace 0 0 [
            uivlist 0 [

                cooldownbar
            ]
        ]
    ]
]

//convenience body of carbine rendering code, as it gets called twice
//it will be called early if some other weapon is out, and late if it is out
//this allows the large weapon (see fontsize, imgx,imgy) to always be in the same location
carbine = [
    if (!= (getweapon) 3) [
        fontsize = 0.01
        imgx = 0.08
        imgy = 0.025
    ] [
        fontsize = 0.7
        imgx = 0.32
        imgy = 0.1
    ]
    uialign -1 1
    uispace $UIwindow_padding 0 [
        uialign -1 1
        uivlist -0.0 [
            uiimage "media/interface/weapon/carbineicon.png" $imgx $imgy
            uitext $uistr_hud_0 $fontsize
        ]
    ]
]

//bottom right weapon bar readout, shows the two weapons in player's possession
newui "weaponbar" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace $UIwindow_padding $UIwindow_padding [
            uialign -1 1
            uihlist $UIwindow_padding [
                if (!= (getweapon) 3) [ //render carbine first if it is not being wielded (needs to be small)
                    (carbine)
                ]
                //set appropriate sizes for main weapon (small if not wielded, large if wielded)
                if (= (getweapon) 3) [
                    fontsize = 0.01
                    imgx = 0.08
                    imgy = 0.025
                ] [
                    fontsize = 0.7
                    imgx = 0.32
                    imgy = 0.1
                ]
                uialign -1 1
                uispace $UIwindow_padding 0 [
                    uialign -1 1
                    uivlist -0.0 [
                        if (= (getcombatclass) 0) [
                            uiimage "media/interface/weapon/rifleicon.png" $imgx $imgy
                            uitext $uistr_hud_1 $fontsize
                        ]
                        if (= (getcombatclass) 1) [
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                            uitext $uistr_hud_2 $fontsize
                        ]
                        if (= (getcombatclass) 2) [
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                            uitext $uistr_hud_3 $fontsize
                        ]
                    ]
                ]
                if (= (getweapon) 3) [ //render carbine later if it is being weilded (so the small primary weapon is on the left)
                    (carbine)
                ]
            ]
        ]
    ]
]

newui "editstats" [
    uiallowinput 0
    if $showhud [
        uialign -1 1
        uiabovehud
        uicolor $c_menu_a 0 0 [
            uispace $UIwindow_padding $UIwindow_padding [
                uivlist 0 [
                    uicontext (
                        format (concatword "%4^f7%5^f7" $uistr_hud_4 " :^f8 %1 ^f7| " $uistr_hud_5 " :^f8 %2 ^f7| " $uistr_hud_6 " :^f8 %3") $gridpower $mapscale $floatspeed (
                            if $hmapedit [format (concatword $uistr_hud_7 "^f8 %1 ^f7:^f8 %2^n") $hbrushindex $hbrushname] [
                                if (! (enthavesel)) [
                                format (concatword $uistr_hud_8 "^f8 %1 ^f7:^f8 %2^n") (max 0 (getseltex)) (gettexname (getseltex))
                                ]
                            ]
                        ) (if (enthavesel) [concat (entget) ":^f8" (enthavesel) (concatword "^f7" $uistr_hud_9 "^n")])
                    )
                    uicontext (format (concatword $uistr_hud_10 " %1%2") $selchildcount (if $showmat [selchildmat ": "]))
                    uihlist 0 [
                        uicontextfill 16 0 [uicontext (format "wtr:%1 (%2%%)" $editstatwtr $editstatvtr)    ; uialign- -1 0]
                        uicontextfill 16 0 [uicontext (format "wvt:%1 (%2%%)" $editstatwvt $editstatvvt)    ; uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 16 0 [uicontext (format "evt:%1 " $editstatevt)                       ; uialign- -1 0]
                        uicontextfill 16 0 [uicontext (format "eva:%1 " $editstateva)                       ; uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta)                     ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (concatword "va:" $editstatva)                        ; uialign- -1 0]
                        uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch) ; uialign- -1 0]
                        uicontextfill  7 0 [uicontext (concatword "oq:" $editstatoq)                        ; uialign- -1 0]
                    ]
                    uialign* -1 0
                ]
            ]
            uioutline $c_line ; uiclamp- 1 1 1 1
        ]
    ]
]

newui "varicons" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace 0.01 0.1 [
            uivlist 0 [
                looplist i (concat $uistr_hud_11 $uistr_hud_12 $uistr_hud_13 $uistr_hud_14 $uistr_hud_15 (? $scoreboardmultiplayer $uistr_hud_16)) [
                    uifont "default_outline" [
                        uialign 1
                        if $$i [
                            uitext (concatword "^f8" $i) 0.47
                        ] [uicolortext $i 0x80FFFFFF 0.47]
                    ]
                ]
            ]
        ]
    ]
]
