// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "config/lang_en.cfg"          // Language Strings
exec "config/ui/lib.cfg"           // UI library
exec "config/ui/style.cfg"         // Styles
exec "config/ui/scoreboard.cfg"    // Scoreboard
exec "config/ui/hud.cfg"           // HUD
exec "config/ui/fkey.cfg"          // F# Key Menus
exec "config/ui/serverbrowser.cfg" // Server Browser
exec "config/ui/options.cfg"       // Options Menu
exec "config/ui/class.cfg"       // Class Menu

reui = [
   exec "config/ui.cfg"
]

togglemainmenu = [|| hidetopui [toggleui main]]

bind ESCAPE [togglemainmenu]

///////////////////////////////////////////////////////////////////////////////
//  Main                                                                     //
///////////////////////////////////////////////////////////////////////////////
UImenu "main" [
    uieschide (! $mainmenu)
    uihlist 0 [
        if ($mainmenu) [
            uivlist 0 [
                UIbutton "" [uitext $uistr_main_0 1] 0.25 0.08 [hideui "main" ; showui "server_browser"]
                uifill 0.25 0.02 [UIbar 1]
                UIbutton "hold2" [uitext $uistr_main_1 0.8] 0.25 0.05 [hideui "main" ; showui "class_selection"]
                UIbutton "hold2" [uitext $uistr_main_2 0.8] 0.25 0.05 [hideui "main" ; showui "player_setup"]
                UIbutton "hold2" [uitext $uistr_main_3 0.8] 0.25 0.05 [hideui "main" ; showui "options"]
                if $mainmenu [
                    UIbutton "hold2" [uitext $uistr_main_4 0.8] 0.25 0.05 [hideui "main" ; showui "credits"]
                ]
                uifill 0.25 0.02 [UIbar 1]
                UIbutton "hold2" [uitext $uistr_main_5 0.75] 0.25 0.04 [quit]
            ]
        ]
        if (! $mainmenu) [
            uivlist 0 [
                if (isspectator $getclientnum) [
                    if $scoreboardmultiplayer [
                        if (ismaster $getclientnum) [
                            UIbutton "hold2" [uitext $uistr_main_ingame_0 0.65] 0.2 0.04 [hideui "main" ; spectator 0]
                        ] [
                            if (> $getmastermode 1) [
                                uifill 0 0.04 [uitext (concatword "^f4" $uistr_main_ingame_0) 0.65]
                            ] [
                                UIbutton "hold2" [uitext $uistr_main_ingame_0 0.65] 0.2 0.04 [hideui "main" ; spectator 0]
                            ]
                        ]
                    ] [
                        UIbutton "hold2" [uitext $uistr_main_ingame_0 0.65] 0.2 0.04 [hideui "main" ; spectator 0]
                    ]
                ] [
                    UIbutton "hold2" [uitext $uistr_main_ingame_1 0.65] 0.2 0.04 [hideui "main" ; spectator 1]
                ]
                case $getteam [
                    0] [uifill 0 0.04] [
                    1] [UIbutton "hold2" [uitext $uistr_main_ingame_2 0.65] 0.2 0.04 [team rojo; spawncombatclass 0]] [
                    2] [UIbutton "hold2" [uitext $uistr_main_ingame_3 0.65] 0.2 0.04 [team azul; spawncombatclass 0]]
                uifill 0.2 0.02 [UIbar 1]
                UIbutton "hold2" [uitext $uistr_main_1 0.65] 0.2 0.04 [hideui "main" ; showui "class_selection"]
                UIbutton "hold2" [uitext $uistr_main_2 0.65] 0.2 0.04 [hideui "main" ; showui "player_setup"]
                UIbutton "hold2" [uitext $uistr_main_3 0.65] 0.2 0.04 [hideui "main" ; showui "options"]
                uifill 0.2 0.02 [UIbar 1]
                UIbutton "hold2" [uitext $uistr_main_ingame_6 0.65] 0.2 0.04 [disconnect]
            ]
        ]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Player Setup                                                             //
///////////////////////////////////////////////////////////////////////////////
UI_pmodel  = 0
UI_pteam   = 0
UI_pweapon = 1

UImenu "player_setup" [
    uivlist 0.01 [
        uialign -2 -1
        uivlist 0.0037 [
            uitext $uistr_player_setup_0 0.7
            uifield name 15 [] 0.7 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
        ]
        uihlist 0.01 [
            looplist3 v t l [
                "playercolorazul" 1 4
                "playercolorrojo" 2 4
            ] [
                uivlist 0.0037 [
                    case $t 1 [
                        uitext "^f8Azul" 0.7
                    ] 2 [
                        uitext "^f3Rojo" 0.7
                    ] () [uitext $uistr_player_setup_1 0.7]
                    uigrid (? (< $t 1) 3 2) 0.005 0.005 [
                        local color
                        uihover [UI_pteam = $t]
                        loop i $l [
                            color = (getplayercolor $i $t)
                            uicolor $color 0.08 0.08 [
                                uioutline (? (= $$v $i) $c_fill $color)
                                uiclamp- 1 1 1 1
                                uipress [$v $i]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [] [] [] $uistr_main_2

///////////////////////////////////////////////////////////////////////////////
//  Credits                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_creditlist = [
    UItitle [
        uispace 0.015 0 [
            uialign -1 -2
            uihlist 0.01 [
                uiimage "media/interface/cube.png" 0.03 0.03
                uitext [@@@@arg1] 0.7
            ]
        ]
    ] 0 0.032
    uispace 0.03 0 [
        uifill 0 0 [
            uiclamp 1 1
            uimodhgradient 0xC0C0C0 0xFFFFFF
            uiclamp- 1 1 1 1
            uispace 0.02 0.01 [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^f8^"" "^"^fr") ": " "^f4: ") 0.69 0.6
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
   ]
   uifill 0 0.02
]

UImenu "credits" [
    uihlist 0.01 [
        uiscroll 0.8 0.8 [
            uifill 0.77
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                UI_creditlist $uistr_credits_0 [
                    [ Alex Foster "@no-lex" ]
                    [ David Chen "@TheEgghead27"]
                    [ Kamil Zelech "@azkoon" ]
                    [ Maxim Therrien "@acerspyro" ]
                    [ Russell Wen "@10010101001" ]
                    [ NoNo "@nonoesimposible" ]
                ]
                UI_creditlist $uistr_credits_1 [
                    [ Alex Foster "@no-lex"]
                    [ David Chen "@TheEgghead27"]
                    [ Kamil Zelech "@azkoon"]
                    [ Luis David Licea Torres "@Luis-Licea"]
                    [ Maxim Therrien "@acerspyro"]
                    [ Sławomir Błauciak "@q009"]
                    [ beam brain "@OlRabiesAbe"]
                    [ Duskhorn "@Duskhorn" ]
                    [ NoNo "@nonoesimposible"]
                ]
                UI_creditlist $uistr_credits_2 [
                    [Lee "eihrul" Salzman]
                    [Benjamin Segovia]
                    [Kevin "Hirato Kirata" Meyer]
                    [Dylan "Dacker" Ackerly]
                    [Sławomir "Q009" Błauciak]
                    [Jannic "Uphys" Böhme]
                    [Shane "Nieb" Nieb]
                    [Daniel "q66" Kolesa]
                    ["RaZgRiZ"]
                    [Wouter "Aardappel" van Oortmerssen]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        UIvscroll 0.02 0.8 2.0
    ]
] [] [] [] $uistr_credits_3

///////////////////////////////////////////////////////////////////////////////
//  Display Changes                                                          //
///////////////////////////////////////////////////////////////////////////////
UImenu "changes" [
    uivlist 0.02 [
        uiclamp 1 1
        UItitle [uitext $uistr_changes_0 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            loop i $pendingchanges [
                uitext (concatword "^f8" (pendingchanges $i)) 0.6
            ]
        ]
        uihlist 0 [
            UIbutton "hold2" [uitext $uistr_changes_1 0.6] 0.18 0.032 [applychanges]
            UIbutton "hold2" [uitext $uistr_changes_2 0.6] 0.18 0.032 [hideui "changes"]
        ]
    ]
] [if (= $pendingchanges 0) [hideui "changes"]]


///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_notepadfile = "untitled.txt"
UI_cmc_from   = ""
UI_cmc_to     = ""

UImenu "notepad" [
    uivlist 0.01 [
        uihlist 0.01 [
            uialign -1 -2
            uifield UI_notepadfile -35 [] 0.6 [
                uioutline (uifocus? 0xFF0000 $c_line)
                uiclamp- 1 1 1 1
            ]
            UIbutton "hold2" [uitext "Load"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textload $UI_notepadfile]
            UIbutton "hold2" [uitext "Save"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textsave $UI_notepadfile]
            UIbutton "hold2" [uitext "Exec"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textexec]
            uispace 0.01 0 [UIbar 0 1]
            UIbutton "hold2" [uitext "Copy"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textcopy]
            UIbutton "hold2" [uitext "Paste"      0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textpaste]
            UIbutton "hold2" [uitext "Select All" 0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textselectall]
            uispace 0.01 0 [UIbar 0 1]
            UIbutton "hold2" [uitext "Clear All"  0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textclear]
        ]
        uitexteditor $UI_notepadfile -80 20 1 "" 0 [
            uioutline (uifocus? 0xFF0000 $c_line)
            uiclamp- 1 1 1 1
        ]
        textinit $UI_notepadfile $UI_notepadfile
    ]
] [] [] [] "Notepad"

notepad = [
    if $numargs [UI_notepadfile = $arg1]
    showui "notepad"
]

mapnotepad = [
    UI_notepadfile = (concatword "media/map/" (? $numargs $arg1 $mapname) ".cfg")
    showui "notepad"
]

copymapcfg = [
    if $numargs [
        UI_cmc_from = $arg1
        UI_cmc_to   = (? (= $numargs 2) $arg2 $mapname)
        if (findfile (concatword "media/map/" $UI_cmc_from ".cfg")) [
            if (findfile (concatword "media/map/" $UI_cmc_to ".cfg")) [showui "cmc_confirm"] [
                textfocus "#copymapcfg"
                textload (concatword "media/map/" $UI_cmc_from ".cfg")
                textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
            ]
        ] [
            echo (concatword "^f7" $uistr_copymapcfg_0 " "^f8^" $UI_cmc_from "^f7^" " $uistr_copymapcfg_1)
            UI_cmc_from = "" ; UI_cmc_to = ""
        ]
    ]
]

UImenu "cmc_confirm" [
    uivlist 0.02 [
        uiclamp 1 1
        UItitle [uitext $uistr_cmc_confirm_0 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            uitext (concatword "^f2" $UI_cmc_from) 0.6
            UItriangle 0 0.01 0.01 180
            uitext (concatword "^f2" $UI_cmc_to) 0.6
        ]
        uihlist 0 [
            UIbutton "hold2" [uitext $uistr_cmc_confirm_1 0.6] 0.18 0.032 [
                textfocus "#copymapcfg"
                textload (concatword "media/map/" $UI_cmc_from ".cfg")
                textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
                hideui "cmc_confirm"
            ]
            UIbutton "hold2" [uitext $uistr_cmc_confirm_2 0.6] 0.18 0.032 [hideui "cmc_confirm"]
        ]
    ]
] [
    if (|| [=s $UI_cmc_from ""] [=s $UI_cmc_to ""]) [hideui "cmc_confirm"]
] [UI_cmc_from = "" ; UI_cmc_to = ""]
